// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/12/Keyboard.jack

/**
 * A library for handling user input from the keyboard.
 */
class Keyboard {

    static String str;
    static int address;
    static int cursor_x, cursor_y;
    static int max;
    /** Initializes the keyboard. */
    function void init() {
        let str = String.new(1);
        let address = 24576;
        let cursor_x = 0;
        let cursor_y = 0;
        let max = 62;
        return;
    } 

    /**
     * Returns the character of the currently pressed key on the keyboard;
     * if no key is currently pressed, returns 0.
     *
     * Recognizes all ASCII characters, as well as the following keys:
     * new line = 128 = String.newline()
     * backspace = 129 = String.backspace()
     * left arrow = 130
     * up arrow = 131
     * right arrow = 132
     * down arrow = 133
     * home = 134
     * End = 135
     * page up = 136
     * page down = 137
     * insert = 138
     * delete = 139
     * ESC = 140
     * F1 - F12 = 141 - 152
     */
    function char keyPressed() {
        var char key;
        let key = Memory.peek(address);
        return key;
    }

    /**								
     * Waits until a key is pressed on the keyboard and released,
     * then echoes the key to the screen, and returns the character 
     * of the pressed key.
     */
    function char readChar() {
        var char push_key;
        // カーソル表示
        do Output.printChar(0);

        while (0 = Keyboard.keyPressed()) {
            // do nothing;
        }
        let push_key = Keyboard.keyPressed();

        while( push_key = Keyboard.keyPressed() ) {
            // キーボードの押下が修了するまで
        }
        
        do Output.backSpace();
        do Output.printChar(push_key);
        return push_key;
    }

    /**								
     * Displays the message on the screen, reads from the keyboard the entered
     * text until a newline character is detected, echoes the text to the screen,
     * and returns its value. Also handles user backspaces.
     */
    function String readLine(String message) {
        var int len, i;
        var String str;
        var char c;
        let str = String.new(max);

        do Output.printString(message);
        while (true) {
            let c = Keyboard.readChar();
            if (c = String.newLine()) {
                do Output.println();
                return str;
            } else {
                if (c = String.backSpace()) {
                    do str.eraseLastChar();
                } else {
                    let str = str.appendChar(c);
                }
            }
        }
        return str;
    }   

    /**								
     * Displays the message on the screen, reads from the keyboard the entered
     * text until a newline character is detected, echoes the text to the screen,
     * and returns its integer value (until the first non-digit character in the
     * entered text is detected). Also handles user backspaces. 
     */
    function int readInt(String message) {
        var char c;
        var String num_str;
        var int number;
        let num_str = Keyboard.readLine(message);
        let number = num_str.intValue();
        return number;
    }
}
