// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/02/ALU.hdl

/**
 * The ALU (Arithmetic Logic Unit).
 * Computes one of the following functions:
 * x+y, x-y, y-x, 0, 1, -1, x, y, -x, -y, !x, !y,
 * x+1, y+1, x-1, y-1, x&y, x|y on two 16-bit inputs, 
 * according to 6 input bits denoted zx,nx,zy,ny,f,no.
 * In addition, the ALU computes two 1-bit outputs:
 * if the ALU output == 0, zr is set to 1; otherwise zr is set to 0;
 * if the ALU output < 0, ng is set to 1; otherwise ng is set to 0.
 */

// Implementation: the ALU logic manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) set x = 0        // 16-bit constant
// if (nx == 1) set x = !x       // bitwise not
// if (zy == 1) set y = 0        // 16-bit constant
// if (ny == 1) set y = !y       // bitwise not
// if (f == 1)  set out = x + y  // integer 2's complement addition
// if (f == 0)  set out = x & y  // bitwise and
// if (no == 1) set out = !out   // bitwise not
// if (out == 0) set zr = 1
// if (out < 0) set ng = 1

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute out = x + y (if 1) or x & y (if 0)
        no; // negate the out output?

    OUT 
        out[16], // 16-bit output
        zr, // 1 if (out == 0), 0 otherwise
        ng; // 1 if (out < 0),  0 otherwise

    PARTS:
    // zxの値によりxの値を決定する
    Not16 ( in=x, out=notx );
    And16 ( a=x, b=notx, out=zerox );
    Mux16 ( a=x, b=zerox, sel=zx, out=zxx );

    // nxの値によりxの値を決定する
    Not16 ( in=zxx, out=nzxx );
    Mux16 ( a=zxx, b=nzxx, sel=nx, out=defx ); 

    // zyの値によりyの値を決定する
    Not16 ( in=y, out=noty );
    And16 ( a=y, b=noty, out=zeroy );
    Mux16 ( a=y, b=zeroy, sel=zy, out=zyy );

    // nyの値によりyの値を決定する
    Not16 ( in=zyy, out=nzyy );
    Mux16 ( a=zyy, b=nzyy, sel=ny, out=defy );

    // fの値により, 昨日を変更する
    Add16 ( a=defx, b=defy, out=sumdxy );
    And16 ( a=defx, b=defy, out=dxandy );
    Mux16 ( a=dxandy, b=sumdxy, sel=f, out=fout );

    // noの値によりfoutの値を反転するか決定
    Not16 ( in=fout, out=nfout );
    Mux16 ( a=fout, b=nfout, sel=no, out[15]=ng, out[0..7]=o1, out[8..15]=o2, out=out );

    // zr, ngの決定を行う
    Or8Way ( in=o1, out=zro1 );
    Or8Way ( in=o2, out=zro2 );
    Or ( a=zro1, b=zro2, out=tmpzr );
    Not ( in=tmpzr, out=zr );
}
